# Copyright (c) 2021 AlgoIdeas
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

template("build_component") {
  if (defined(invoker.version)) {
    print(invoker.version)
  }
  action(target_name) {
    deps = []
    if (defined(invoker.deps)) {
      deps += invoker.deps
    }
    args = []
    if (defined(invoker.exec_path)) {
      args += [ "--path=${invoker.exec_path}" ]
    }
    if (defined(invoker.enable)) {
      args += [ "--enable=${invoker.enable}" ]
    }
    if (defined(invoker.prebuilts)) {
      args += [ "--prebuilts=${invoker.prebuilts}" ]
    }
    if (defined(invoker.command)) {
      args += [ "--command=${invoker.command}" ]
    }

    # external component build log
    target_dir = rebase_path("${target_out_dir}/Build.log")
    args += [ "--target_dir=${target_dir}" ]

    # external component error log if compile failed
    out_dir = rebase_path("${root_out_dir}/error.log")
    args += [ "--out_dir=${out_dir}" ]
    script = "//Build/scripts/Build_components.py"
    outputs = [ "$target_out_dir/${target_name}_build_ext_components.txt" ]
  }
}

